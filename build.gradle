plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

if (JavaVersion.current() < JavaVersion.VERSION_1_8) {
    throw new GradleException("This build must be run with java 8 or higher. Current version is " + JavaVersion.current() + ".\nYou should install jdk >= 1.8 from http://www.oracle.com/technetwork/java/javase/downloads and/or check your JAVA_HOME.")
}

group 'fr.spacefox'
archivesBaseName = 'confusable-homoglyphs'
version '1.1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.2'
    mockitoVersion = '4.2.0'
}

dependencies {
    implementation group: 'org.jetbrains', name: 'annotations', version: '23.0.0'

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: project.ext.mockitoVersion
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: project.ext.mockitoVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.ext.junitVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: project.ext.junitVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.ext.junitVersion
    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

jacoco {
    toolVersion = '0.8.7'
}
jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'fr/spacefox/confusablehomoglyphs/Cli.class'
            ])
        }))
    }
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
}

tasks.check.dependsOn jacocoTestReport
tasks.check.dependsOn jacocoTestCoverageVerification

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'Confusable Homoglyphs'
                description = 'A tool to deal with Unicode characters that looks like each other an can be confused - a.k.a confusable homoglyphs'
                url = 'https://github.com/SpaceFox/confusable-homoglyphs'
                scm {
                    connection = 'scm:git:https://github.com/SpaceFox/confusable-homoglyphs.git'
                    developerConnection = 'scm:svn:https://github.com/SpaceFox/confusable-homoglyphs.git'
                    url = 'https://github.com/SpaceFox/confusable-homoglyphs.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'SpaceFox'
                        name = 'SpaceFox'
                        email = 'SpaceFox@users.noreply.github.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = project.ossrhUsername
                password = project.ossrhPassword
            }
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
